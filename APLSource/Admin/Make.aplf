 Make flag;M;C;path;zipFilename;res;parms;cfg
⍝ "Make" a new version of this projects
 :If flag
     :If 0=⎕SE.⎕NC'MakeHelpers'
         {}⎕SE.Tatin.LoadPackages'[tatin]MakeHelpers'⎕SE
     :EndIf
     M←⎕SE.MakeHelpers
     C←##.CiderConfig
     ⎕←'' ⋄ 0 0⍴⍞,0/⍞←'Pause sync services like Dropbox, Google Drive etc. in order to avoid FILE ACCESS errors. Press <enter> to continue '
     :If ⎕NULL≡cfg←M.EditPackageConfigFile C.HOME
         ⎕←'Cancelled by user' ⋄ →0
     :EndIf
     path←C.HOME,'/',C.CIDER.distributionFolder
     M.RecreateFolder path
     M.ConvertMarkdownToHtml5 C.HOME∘,¨'/docs/' '/html/'
     (C.HOME,'/html/')∘{⍺ ⎕NCOPY⍠('IfExists' 'Replace')⊢⍵}¨M.##.F.ListFiles C.HOME,'/docs/*.png'
     path ⎕NCOPY C.HOME,'/html'
     parms←⎕SE.Tatin.CreateBuildParms C.HOME
     parms.targetPath←C.CIDER.distributionFolder
     parms.version←cfg.version
     zipFilename←⎕SE.Tatin.BuildPackage parms
     ⎕←'*** New version build successfully:',M.CR,'   ',zipFilename
 :EndIf
