 r←Test_Run_01(debugFlag batchFlag);log;debugFlag;⎕TRAP;rc;ref2Gui;T2
⍝ Exercise the `Run` method
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 r←T._Failed

 T2←⎕NEW ##.Tester2 TestCasesSimu
 T2.TestFlag←1
 (rc log)←T2.Run 1
 →T.FailsIf 0=rc
 →T.PassesIf 1=+/'Looking for a function "Initial": found and executed successfully'⍷∊log  ⍝ One function `Initial`
 →T.PassesIf 1=+/'Looking for a function "Cleanup":'⍷∊log  ⍝ One function `Cleanup`
 →T.PassesIf 2=+/≢'Searching for INI file .* not found'⎕S 1⊣log ⍝ Two INI files
 →T.PassesIf∨/'14 test cases of 14 called'⍷∊log
 →T.PassesIf∨/'3 test cases failed (flagged with "*")'⍷∊log
 →T.PassesIf∨/'0 test cases broken'⍷∊log

 r←T._OK
