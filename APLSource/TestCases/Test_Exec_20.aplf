 r←Test_Exec_20(debugFlag batchFlag);⎕TRAP;rc;log;QDMX
⍝ Test `exec_after_each_test` without returning a result
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(888 'C' '→∆Error')(0 'N')
 r←T._Failed

 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.∆flag←0
 #.TEMP.⎕FX'r←Test_01 flags;⎕TRAP' 'r←T._Failed' '∆flag←1'
 #.TEMP.⎕FX'r←Test_02 flags;⎕TRAP' 'r←T._OK'
 #.TEMP.⎕FX'r←Test_03 flags;⎕TRAP' 'r←T._OK'
 #.TEMP.⎕FX'∆Check arg;⎕TRAP' '→(0=∆flag)/0' '∘∘∘ ⍝ exec_after_each_test'
 #.TEMP.T←⎕NEW ##.Tester2 #.TEMP
 #.TEMP.T.exec_after_each_test←'#.TEMP.∆Check'

 :Trap 2
     (rc log)←#.TEMP.T.Run 1
     →T.FailsIf 1
 :Else
     QDMX←⎕DMX
     →T.PassesIf∨/'exec_after_each_test'⍷2⊃QDMX.DM
 :EndTrap

 r←T._OK
⍝Done
