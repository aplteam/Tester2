 r←Test_Run_05(stopFlag batchFlag);T2;expected;log;parms;qdmx;rc;ref;stopFlag;⎕TRAP
⍝ Exercise the `RunGUI` method.
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 r←T._Failed

 T2←⎕NEW ##.Tester2 TestCasesSimu
 T2.TestFlag←1
 ⎕TRAP←(1001 'E' '→⎕lc')(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 :If 'Win'≡##.APLTreeUtils2.GetOperatingSystem''
     (rc log)←T2.RunGUI 2
     T2.CloseGUI
     expected←'  1 test case of 1 called' '  0 test cases failed' '  0 test cases broken'
     →T.FailsIf expected≢¯3↑¯3↓log

     T2←⎕NEW ##.Tester2 TestCasesSimu
     T2.TestFlag←1
     (rc log)←T2.RunGUI'Grp1'
     T2.CloseGUI
     →T.FailsIf 3 0 0≢∆GetTotals log

     T2←⎕NEW ##.Tester2 TestCasesSimu
     T2.TestFlag←1
     (rc log)←T2.RunGUI'Test_Grp1'
     T2.CloseGUI
     →T.FailsIf 3 0 0≢∆GetTotals log

     T2←⎕NEW ##.Tester2 TestCasesSimu
     parms←T2.CreateParms ⍬
     T2.TestFlag←1
     parms.trapFlag←0
     parms.debugFlag←1
     (rc log)←parms T2.RunGUI'Test_Grp1'
     ref←T2.Getref2Gui
     →T2.PassesIf 0=ref.∆n.TrapErrors.State
     →T2.PassesIf 1=ref.∆n.Debug.State
     T2.CloseGUI
     →T.FailsIf 3 0 0≢∆GetTotals log

     r←T._OK
 :Else
     :Trap 0
         (rc log)←T2.RunGUI 2
         →T.FailsIf 1
     :Else
         qdmx←⎕DMX
         T.PassesIf'RunGUI is Windows-only'≡qdmx.EM
     :EndTrap
 :EndIf
⍝Done
