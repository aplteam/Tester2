 r←Test_Init_05(debugFlag batchFlag);Foo;ref;rc;log;⎕TRAP
⍝ Exercise `Run` with an `Initial_foo` function that returns 0 (failed) and check whether `Cleanup_foo` was executed
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕SHADOW'Temp'
 'Temp'#.⎕NS''
 #.Temp.I_was_here←0
 #.Temp.T2←⎕NEW ##.Tester2 #.Temp
 #.Temp.⎕FX'r←Initial_foo' '∆Testing←0' 'r←0'
 #.Temp.⎕FX'r←Test_foo_01 x;⎕TRAP' '∘∘∘'                               ⍝ Not to be executed
 #.Temp.⎕FX'r←Test_misc_01 x;⎕TRAP' '#.Temp.I_was_here←1' 'r←T2._OK'   ⍝ Not to be executed
 #.Temp.⎕FX'Cleanup_foo' '⎕EX ''∆Testing'''
 (rc log)←#.Temp.T2.Run 1
 →T.PassesIf∨/'Function "Initial_foo" found and executed but did not return a 1'⍷∊log
 →T.PassesIf 0=rc
 →T.PassesIf 0=#.Temp.⎕NC'∆Testing'

 r←T._OK
