 r←Test_IniFiles_03(stopFlag batchFlag);log;stopFlag;⎕TRAP;rc;ini;body;report;iniFilename1;iniFilename2
⍝ Exercise two INI files: "testcases.ini" & "testcases_{computername}.ini"
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 r←T._Failed

 iniFilename1←'Testcases.ini'
 FilesAndDirs.DeleteFile iniFilename1
 ini←'[Config]' 'foo = 1' 'goo = ''Hello'''
 {(⊂⍵)FilesAndDirs.NPUT ⍺}/iniFilename1 ini

 iniFilename2←'Testcases-',(2 ⎕NQ'#' 'GetEnvironment' 'Computername'),'.ini'
 FilesAndDirs.DeleteFile iniFilename2
 ini←'[Config]' 'foo = 2' 'too = ''Universe'''
 {(⊂⍵)FilesAndDirs.NPUT ⍺}/iniFilename2 ini

 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.T←⎕NEW ##.Tester2 #.TEMP
 body←⊂'r←Test_03(stopFlag batchFlag);⎕TRAP'
 body,←⊂'⎕TRAP←(999 ''C'' ''∘∘∘ ⍝ Deliberate error'')(0 ''N'')'
 body,←⊂'r←T._Failed'
 body,←⊂'→T.FailsIf 2≠T.INI.⎕NC''foo'''
 body,←⊂'→T.FailsIf 2≠T.INI.⎕NC''goo'''
 body,←⊂'→T.FailsIf 2≠T.INI.⎕NC''too'''
 body,←⊂'→T.FailsIf T.INI.foo≢2'
 body,←⊂'→T.FailsIf T.INI.goo≢''Hello'''
 body,←⊂'→T.FailsIf T.INI.too≢''Universe'''
 body,←⊂'→T.FailsIf 3≠1↑≢T.INI.List'
 body,←⊂'r←T._OK'
 #.TEMP.⎕FX body
 #.TEMP.T.TestFlag←1
 (rc report)←#.TEMP.T.Run 1
 →T.GoToTidyUp 1≠+/'Searching for INI file Testcases.ini: found'⍷∊report
 →T.GoToTidyUp 1≠+/('Searching for INI file Testcases-',(2 ⎕NQ'#' 'GetEnvironment' 'Computername'),'.ini: found')⍷∊report
 →T.GoToTidyUp 1≠+/'1 test case of 1 called'⍷∊report
 →T.GoToTidyUp 1≠+/'0 test cases failed'⍷∊report
 →T.GoToTidyUp 1≠+/'0 test cases broken'⍷∊report

 r←T._OK

∆TidyUp:
 :Trap 0 ⋄ FilesAndDirs.DeleteFile iniFilename1 ⋄ :EndTrap
 :Trap 0 ⋄ FilesAndDirs.DeleteFile iniFilename2 ⋄ :EndTrap
