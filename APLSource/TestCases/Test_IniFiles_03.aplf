 r←Test_IniFiles_03(stopFlag batchFlag);log;stopFlag;⎕TRAP;rc;ini;body;report;iniFilename1;iniFilename2
⍝ Exercise two INI files: "testcases.ini" & "testcases_{computername}.ini"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 ini←'[Config]' 'foo = 1' 'goo = ''Hello'''
 iniFilename1←'Testcases.ini'
 ##.APLTreeUtils.WriteUtf8File iniFilename1 ini

 ini←'[Config]' 'foo = 2' 'too = ''Universe'''
 iniFilename2←'Testcases_',##.WinSys.GetComputerName,'.ini'
 ##.APLTreeUtils.WriteUtf8File iniFilename2 ini

 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.T←⎕NEW ##.Tester2 #.TEMP
 body←⊂'r←Test_03(stopFlag batchFlag);⎕TRAP'
 body,←⊂'⎕TRAP←(999 ''C'' ''. ⍝ Deliberate error'')(0 ''N'')'
 body,←⊂'r←T._Failed'
 body,←⊂'→T.FailsIf 2≠T.INI.⎕NC''foo'''
 body,←⊂'→T.FailsIf 2≠T.INI.⎕NC''goo'''
 body,←⊂'→T.FailsIf 2≠T.INI.⎕NC''too'''
 body,←⊂'→T.FailsIf T.INI.foo≢2'
 body,←⊂'→T.FailsIf T.INI.goo≢''Hello'''
 body,←⊂'→T.FailsIf T.INI.too≢''Universe'''
 body,←⊂'→T.FailsIf 3≠1↑≢T.INI.List'
 body,←⊂'r←T._OK'
 #.TEMP.⎕FX body
 #.TEMP.T.TestFlag←1
 report←#.TEMP.T.Run 1
 →T.GoToTidyUp 1≠+/'INI file(s) "Testcases.ini,testcases_'⍷∊report
 →T.GoToTidyUp 1≠+/'1 test case of 1 executed'⍷∊report
 →T.GoToTidyUp 1≠+/'0 test cases failed'⍷∊report
 →T.GoToTidyUp 1≠+/'0 test cases broken'⍷∊report

 r←T._OK

∆TidyUp:
 ##.FilesAndDirs.DeleteFile iniFilename1 iniFilename2
