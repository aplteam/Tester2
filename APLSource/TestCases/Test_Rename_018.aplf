 r←Test_Rename_018(stopFlag batchFlag);stopFlag;⎕TRAP;T2;tempDir;res;expected
⍝ Rename a group into an existing group
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')

 r←T._Failed

⍝ #.⎕SHADOW'TEMP'  ⍝ Link gets confused by this
 'TEMP'#.⎕NS''
 T2←⎕NEW ##.Tester2 #.TEMP
 #.TEMP.⎕FX↑'r←Test_Foo_001 dummy' 'r←1'
 #.TEMP.⎕FX↑'r←Test_Foo_002 dummy' 'r←2'
 #.TEMP.⎕FX↑'r←Test_GooZoo_002 dummy' 'r←3'
 #.TEMP.⎕FX↑'r←Test_GooZoo_004 dummy' 'r←4'
 tempDir←FilesAndDirs.GetTempSubDir⊃⎕SI
 res←⎕SE.Link.Create'#.TEMP'tempDir
 Assert'Linked: '{⍺≡(≢⍺)↑⍵}res

 'Foo'T2.RenameTestFnsTo'GooZoo'
 →T.GoToTidyUp 0 0≢#.TEMP.⎕NC↑'Test_Foo_001' 'Test_Foo_002'
 →T.GoToTidyUp 3 3≢#.TEMP.⎕NC↑'Test_GooZoo_001' 'Test_GooZoo_005'
 expected←'Test_GooZoo_001.aplf' 'Test_GooZoo_002.aplf' 'Test_GooZoo_004.aplf' 'Test_GooZoo_005.aplf'
 →T.GoToTidyUp expected≢{⊃,/1↓⍵}¨⎕NPARTS¨FilesAndDirs.ListFiles tempDir
 →T.GoToTidyUp 1 3 4 2≢#.TEMP.{⍎¨(↓⎕NL 3),¨⊂' ⍬'}⍬

 r←T._OK

∆TidyUp:
 :Trap 0 ⋄ {}⎕SE.Link.Break'#.TEMP' ⋄ :EndTrap
 #.⎕EX'TEMP'
 :Trap 0 ⋄ {}FilesAndDirs.RmDirByForce tempDir ⋄ :EndTrap
⍝Done
