 r←Test_Misc_18(stopFlag batchFlag);rc;list;tmp;expected;⎕TRAP;T2
⍝ Exercise the `ListGroups` and the `ListTestFunctions` methods.
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 r←T._Failed

⍝ Pre-conditions
 tmp←⎕NS''
 tmp.⎕FX'r←Test_001 (x y)' '   ⍝ This and that ' 'r←0'
 tmp.⎕FX'r←Test_002 (x y)' '   ⍝ that and this ' 'r←0'
 tmp.⎕FX'r←Test_003 (x y)' '   ⍝ Something else' 'r←0'
 tmp.⎕FX'r←Test_Grp1_001 (x y)' 'r←0'
 tmp.⎕FX'r←Test_Grp1_002 (x y)' 'r←0'
 tmp.⎕FX'r←Test_Grp1_003 (x y)' 'r←0'
 tmp.⎕FX'r←Test_Grp2_001 (x y)' 'r←0'
 tmp.⎕FX'r←Test_Grp2_002 (x y)' 'r←0'
 tmp.⎕FX'r←Test_Grp3_AAA_001 (x y)' 'r←0'
 tmp.⎕FX'r←Test_Grp3_AAA_002 (x y)' 'r←0'
 tmp.⎕FX'r←Test_Grp3_BBB_001 (x y)' 'r←0'
 tmp.⎕FX'r←Test_Grp4_CCC_001 (x y)' 'r←0'
 T2←⎕NEW ##.Tester2 tmp

⍝ The tests
 →T.PassesIf(⊃⍴T2.ListTestFunctions'')=≢'T'tmp.⎕NL 3
 →T.PassesIf(+/'Test_Grp'{((≢⍺)↑[2]⍵)∧.=⍺}'T'tmp.⎕NL 3)=≢T2.ListTestFunctions'Grp*'
 →T.PassesIf 5=1↑⍴T2.ListGroups

 r←T._OK
