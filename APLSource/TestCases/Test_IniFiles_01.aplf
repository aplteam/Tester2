 r←Test_IniFiles_01(stopFlag batchFlag);log;stopFlag;⎕TRAP;rc;ini;body;report;iniFilename;T2
⍝ Exercise single INI file "testcase.ini"
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 r←T._Failed

 iniFilename←'Testcases.ini'
 FilesAndDirs.DeleteFile iniFilename
 ini←'[Config]' 'foo = 1' 'goo = ''Hello'''
 {(⊂⍵)FilesAndDirs.NPUT ⍺}/iniFilename ini

 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.T←⎕NEW ##.Tester2 #.TEMP
 #.TEMP.⎕EX'Test_000'
 body←⊂'r←Test_01(stopFlag batchFlag);⎕TRAP'
 body,←⊂'⎕TRAP←(999 ''C'' ''∘∘∘ ⍝ Deliberate error'')(0 ''N'')'
 body,←⊂'r←T._Failed'
 body,←⊂'→T.FailsIf 2≠T.INI.⎕NC''foo'''
 body,←⊂'→T.FailsIf 2≠T.INI.⎕NC''goo'''
 body,←⊂'→T.FailsIf T.INI.foo≢1'
 body,←⊂'→T.FailsIf T.INI.goo≢''Hello'''
 body,←⊂'r←T._OK'
 #.TEMP.⎕FX body
 #.TEMP.T.TestFlag←1
 #.TEMP.T.IniFolder←''
 (rc report)←#.TEMP.T.Run 1
 →T.GoToTidyUp 1≠+/'Searching for INI file Testcases.ini: found'⍷∊report
 →T.GoToTidyUp 1≠+/('Searching for INI file Testcases-',(2 ⎕NQ'#' 'GetEnvironment' 'Computername'),'.ini: not found')⍷∊report
 →T.GoToTidyUp 1≠+/'1 test case of 1 called'⍷∊report
 →T.GoToTidyUp 1≠+/'0 test cases failed'⍷∊report
 →T.GoToTidyUp 1≠+/'0 test cases broken'⍷∊report

 r←T._OK

∆TidyUp:
 :Trap 0 ⋄ FilesAndDirs.DeleteFile iniFilename ⋄ :EndTrap
⍝Done
